#
# Executes commands at the start of an interactive session.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Source Prezto.
if [[ -s "${ZDOTDIR:-$HOME}/.zprezto/init.zsh" ]]; then
  source "${ZDOTDIR:-$HOME}/.zprezto/init.zsh"
fi

# Customize to your needs...

# Reminder this is in a public repo!

export ZLE_REMOVE_SUFFIX_CHARS=$' \t\n;&|'
export ZLE_SPACE_SUFFIX_CHARS=$'&|'

bindkey "^[[5~" history-beginning-search-backward
bindkey "^[[6~" history-beginning-search-forward

if [[ -f /etc/ad-release ]]; then
  # running on airserver device
  source /etc/ad-release

  # TODO: use on device helpers
  # TODO: add helpers to finish setup
  # TODO: add helpers to check/switch update channel

  function airserver_read_config() {
    local SECTION="$1"
    local KEY="$2"
    sed -rn '/^\[/{h;d};G;s/^'"$KEY"'=(.*)\n\['"$SECTION"'\]$/\1/p' /media/data/airserver/.config/App\ Dynamic/AirServer.conf 2>/dev/null
  }

  function airserver_read_dbus() {(
    set -o pipefail
    local SECTION="$1"
    local KEY="$2"
    busctl get-property --json=short com.appdynamic.AirServer /config/raw/${SECTION} com.appdynamic.Properties.${SECTION} ${KEY} 2>/dev/null | jq --raw-output .data
  )}

  # for older devices
  if ! type jq > /dev/null; then
     echo "jq missing disabling airserver_read_dbus"
     function airserver_read_dbus() {(
       exit 1 # jq not available
     )}
  fi

  function airserver_write_dbus_string() {
    local SECTION="$1"
    local KEY="$2"
    local STRING="$3"
    busctl set-property com.appdynamic.AirServer /config/raw/${SECTION} com.appdynamic.Properties.${SECTION} ${KEY} s "${STRING}"
  }

  function airserver_get_name() {
    airserver_read_dbus 'Device' 'name' || airserver_read_config 'Device' 'name'
  }

  function airserver_set_name() {
    airserver_write_dbus_string 'Device' 'name' "$*"
  }

  function airserver_build_info() {
    # check if dirty build
    local DIRTY=
    if [[ "$GIT_SHA" == *-dirty ]]; then
      DIRTY=" %F{196}dirty"
    fi

    # check if new build less than 12 hour old then time is more useful than the git sha
    local NOW=$(date +%s)
    if ((($NOW - $BUILD_DATE) < 12*60*60)); then
      local BUILD_TIME=$(date --date="@${BUILD_DATE}" '+%k:%M:%S')
      echo "%F{#c79f1c}$AIRSERVER_VERSION %F{#1cc7c1}$BUILD_TIME$DIRTY"
    else
      echo "%F{#c79f1c}$AIRSERVER_VERSION%F{reset} : %F{#bc1bc2}${GIT_SHA:s/-dirty//}$DIRTY"
    fi
  }

  RPROMPT='%B%F{#4287f5}$(airserver_get_name)%F{reset} : $(airserver_build_info)'

  alias airserver-shell='su -l airserver'
  alias update_prezto="zsh /root/.zprezto-manager update && source .zshrc"

  path+=(/usr/share/ad/tools)

  local static_tools_path=/media/data/static-tools
  path=("$static_tools_path" $path)
  if [[ "$UID == 0" ]]; then
    function update_static_tools() {
      local URL="https://download.airserver.com/static-tools/static-tools.tar.gz"
      if resolvectl domain | grep -q turninn.appdynamic.com; then
        URL="http://staging.turninn.appdynamic.com/static-tools/static-tools.tar.gz"
      fi
      curl -s "$URL" | gunzip | tar xv -C "$static_tools_path" -f -
      rehash
    }
    if [[ ! -d "$static_tools_path" ]]; then
      echo "Use update_static_tools to download static tools bundle"
      mkdir "$static_tools_path"
    fi
  fi
fi

# disable annoying systemd pager
export SYSTEMD_PAGER=

